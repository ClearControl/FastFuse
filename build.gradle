
plugins {
    id "com.jfrog.bintray" version "1.2"
    id "com.diffplug.gradle.spotless" version "3.1.0"
    id "de.undercouch.download" version "3.4.3"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'maven-publish'


// Master gradle file:
apply from: 'https://github.com/ClearControl/master/blob/master/master.gradle?raw=true'

//***********************************************************************************
// JAVA CODE BUILDING

sourceSets
{
    main
    {
        java
        { srcDir 'src/main/java' }
        resources
        { srcDir 'src/main/java' }
    }
    test
    {
        java
        { srcDir 'src/test/java' }
        resources
        { srcDir 'src/test/java' }
    }
}


sourceCompatibility = 1.8
targetCompatibility = 1.8

test 
{
    testLogging.showStandardStreams = true 
    testLogging 
    {
        events "passed", "skipped", "failed"
    }

    exclude '**/demo/**'

    maxHeapSize = "16G"
}

configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies 
{
    // commons lang:
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-math3:3.6.1'

    
    // Vecmath:
    compile ('javax.vecmath:vecmath:1.5.2')
    
    // Symbrio:
    compile ('net.clearcontrol:simbryo:'+depver('simbryo'))    {transitive= true; changing= true}
    
        
    // JUnit:
    compile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}


repositories 
{
    // Main repos:
    mavenLocal()
    mavenCentral()
    jcenter()


    maven { url "http://oss.sonatype.org/content/groups/public"} 
    maven { url "http://mvnrepository.com/artifact" }

    // ClearControl:
    maven { url  "http://dl.bintray.com/clearcontrol/ClearControl"  }
    maven { url "http://dl.bintray.com/haesleinhuepf/snapshots" }

    // ClearVolume: 
    maven { url  "http://dl.bintray.com/clearvolume/ClearVolume"  }
    
    //Sqeazy:
    maven {url  "http://dl.bintray.com/psteinb/maven" }
    
    // ImageJ repo:
    maven {url  "http://maven.imagej.net/content/repositories/releases/"}
    
    // Bioformats:
    maven {url "http://artifacts.openmicroscopy.org/artifactory/maven"}

}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
  }



//***********************************************************************************
// PUBLISHING

group   = project.ext.groupname
version = project.ext.versionsMap[project.name]

artifacts
        {
            archives sourcesJar
            archives javadocJar
        }

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar { classifier "sources" }
        }
    }
}

